h1. puppet-php

@puppet-php@ is a puppet module to manage *PHP* on your systems.
You can manage the *CLI*, *Apache mod_php* and *FPM* version of PHP.

Forked from "saz/puppet-php":https://github.com/saz/puppet-php.


h2. How to use

h3. CLI

bc. include php


h3. Apache with mod_php

bc. include php::apache2

*php::apache2* will include php automatically.


h3. PHP-FPM

bc. include php::fpm

It will also create a @service@ with a Puppet name of @php-fpm@.
But you should not use it directly. To restart php-fpm, use:

bc. notify => Class['php::fpm::service']

*php::fpm* will include @php@ automatically.

To use FPM you need to create at least one pool :

bc. php::fpm::pool { www-data:
}

Parameters and default values:

* $user = 'www-data'
* $group = 'www-data'
* $listen = '/var/run/php-www-data.sock'
* $pm_max_children = 5
* $pm_start_servers = 2
* $pm_min_spare_servers = 1
* $pm_max_spare_servers = 3


h2. Typical Development Usage

bc.   $developer = 'ceefour'
  package { php-doc: ensure => present }
  file { '/etc/apache2/conf.d/php-doc':
    ensure  => file,
    owner   => 'root',
    group   => 'root',
    content => 'Alias /php-doc /usr/share/doc/php-doc/html/',
    notify  => Service['httpd']
  }
  augeas { php-fpm:
    context => '/files/etc/php5/fpm/php.ini',
    changes => [
      'set PHP/max_execution_time     900',
      'set PHP/memory_limit           256M',
      'set PHP/error_reporting        "E_ALL | E_STRICT"',
      'set PHP/display_errors         On',
      'set PHP/display_startup_errors On',
      'set PHP/log_errors             On',
      'set PHP/html_errors            On',
      'set PHP/docref_root            http://localhost/php-doc/',
      'set PHP/docref_ext             .html',
      'set PHP/error_log              php_errors.log',
      'set PHP/post_max_size          8M',
      'set PHP/upload_max_filesize    8M',
    ],
    notify => Class['php::fpm::service']
  }
  include php::fpm
  php::module {
    curl:   notify => Class['php::fpm::service'];
    gd:     notify => Class['php::fpm::service'];
    mysql:  notify => Class['php::fpm::service'];
    mcrypt: notify => Class['php::fpm::service'];
    xmlrpc: notify => Class['php::fpm::service'];
    xdebug: notify => Class['php::fpm::service'];
  }
  php::module {
    apc:  package_prefix => 'php-', notify => Class['php::fpm::service'];
    soap: package_prefix => 'php-', notify => Class['php::fpm::service'];
    pear: package_prefix => 'php-', notify => Class['php::fpm::service'];
  }
  php::fpm::pool { "$developer":
    user   => $developer,
    group  => $developer,
    listen => "/var/run/php-${developer}.sock"
  }


h2. Typical Production Usage

bc.   augeas { php-fpm:
    context => '/files/etc/php5/fpm/php.ini',
    changes => [
      'set PHP/max_execution_time     900',
      'set PHP/memory_limit           256M',
      'set PHP/error_reporting        "E_ALL & ~E_DEPRECATED"',
      'set PHP/display_errors         On',
      'set PHP/display_startup_errors On',
      'set PHP/log_errors             On',
      'set PHP/html_errors            On',
      'set PHP/error_log              php_errors.log',
      'set PHP/post_max_size          8M',
      'set PHP/upload_max_filesize    8M',
    ],
    notify => Class['php::fpm::service']
  }
  include php::fpm
  php::module {
    curl:   notify => Class['php::fpm::service'];
    gd:     notify => Class['php::fpm::service'];
    mysql:  notify => Class['php::fpm::service'];
    mcrypt: notify => Class['php::fpm::service'];
    xmlrpc: notify => Class['php::fpm::service'];
  }
  php::module {
    apc:  package_prefix => 'php-', notify => Class['php::fpm::service'];
    soap: package_prefix => 'php-', notify => Class['php::fpm::service'];
    pear: package_prefix => 'php-', notify => Class['php::fpm::service'];
  }
  php::fpm::pool { magento:
    user                 => magento,
    group                => magento,
    listen               => "/var/run/php-magento.sock",
    pm_max_children      => 100,
    pm_start_servers     => 30,
    pm_min_spare_servers => 10,
    pm_max_spare_servers => 30
  }


h2. Installing PHP Modules

The easiest way of installing additional PHP modules is using packages
from your distribution and the bundled configuration files.

bc. php::module { snmp:
  notify => [ Class['php::fpm::service'], Service['apache2'] ],
}

The default package prefix is @php5-@ e.g. php5-gd. You can use other prefix:

bc. php::module { 'apc': 
  package_prefix => 'php-',
  notify         => [ Class['php::fpm::service'], Service['apache2'] ],
}

Multiple modules are possible, too.

bc. php::module { ['snmp', 'xdebug']:
  notify => Class['php::fpm::service'],
}

Sometimes, modules require some custom configuration settings and you
want to retrieve a file from the server.

bc. php::module { snmp:
  source => true,
  notify => Class['php::fpm::service']
}

At first, this looks a bit strange. If you set `source` to `true`,
a file will be fetched from multiple sources:

1. @puppet:///files/${::fqdn}/etc/php5/conf.d/module.ini@
2. @puppet:///files/${::hostgroup}/etc/php5/conf.d/module.ini@
3. @puppet:///files/${::domain}/etc/php5/conf.d/module.ini@
4. @puppet:///files/global/etc/php5/conf.d/module.ini@

The first source that exists will be used.
This makes it quite easy to have different files for different systems without
duplicating any of your definitions.

This is even possible, if you manage multiple modules!


But if you really need to set a different source, this is possible, too.

bc. php::module { snmp:
  source => 'puppet:///files/different/path/to/the/file/',
  notify => Class['php::fpm::service'],
}

To make it still possible, to have multiple modules, this should point to a directory.
In this directory, place files named `module.ini`.

**Do not forget to add a trailing slash!**

Sometimes you may need to use a template instead of a file.

bc. php::module { snmp:
  content => 'php5/conf.d/',
  notify  => Class['php::fpm::service'],
}

You can define multiple modules, exactly like before with sources.
The only difference is, that, at the moment, only one template per module will be used.

Place your templates inside your template directory and name them `module.ini.erb`


h2. Additional configuration settings

You can place additional configuration files in the @conf.d@ directory as follows:

bc. php::conf { global:
  source => "puppet:///files/php/global.ini",
}

The same source fetching rules applies as in the 'modules' section.


h2. Extra configuration files

Those configuration files will be placed inside an 'extras' directory in your configuration root.
This is to make sure, that those configuration files are not parsed by PHP by default.

bc. php::extra { lite_php_browscap:
  source  => 'puppet:///files/php5/extra/lite_php_browscap.ini',
  require => Php::Conf['browscap'],
  notify  => Class['php::fpm::service'],
}

You can use a template, too:

bc. php::extra { lite_php_browscap:
  content => 'php5/conf.d/',
  require => Php::Conf['browscap'],
  notify  => Class['php::fpm::service'],
}

Again, the same source fetching rules applies as in the 'modules' section.

For more information, see @EXAMPLE@


h2. Service Notification

On every resource, you can define, what other service should be notified.
If you run PHP within Apache, you want to notify Apache of any changes or
FPM should be notified and restarted to make the new configuration work.


h2. Requirements

* php::apache2 requires "apache module":https://github.com/soluvas/puppet-apache.


h2. TODO

* Manage FPM configuration (global settings)
